{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "b652c146-72ae-40a3-8926-62f74a94abee",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Combinatorics\n",
    "using DoubleFloats\n",
    "using IterTools\n",
    "using LinearAlgebra\n",
    "using Random\n",
    "using RandomMatrices\n",
    "using SpecialFunctions\n",
    "using Test"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "3387bbc8-1990-4bb8-8b16-45e43d923b0f",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    random_covariance(N; hbar=2, pure=false)\n",
    "\n",
    "Generate random covariance matrix of a Gaussian state.\n",
    "\n",
    "### Input\n",
    "- `N`  -- Int: Number of modes\n",
    "- `hbar` -- Float: (optional, default: `hbar=2`) value of ``\\\\hbar`` in the commutation relation ``[x,p]=i\\\\hbar``\n",
    "- `pure` -- Bool: (optional, default: `pure=false`) if true, generates a matrix corresponding to a pure Gaussian state\n",
    "### Output\n",
    "\n",
    "Array: ``2N \\\\times 2N`` random covariance matrix.\n",
    "\"\"\"\n",
    "function random_covariance(N; hbar=2, pure=false)\n",
    "    U = rand(Haar(2), N)\n",
    "    O = [real(U) -imag(U); imag(U) real(U)]\n",
    "    U = rand(Haar(2), N) \n",
    "    P = [real(U) -imag(U); imag(U) real(U)]\n",
    "    r = abs.(randn(N))\n",
    "    sq = diagm(vcat(exp.(-r), exp.(r)))\n",
    "    S = O * sq * P \n",
    "    if pure \n",
    "        return (hbar / 2) .* (S * transpose(S))\n",
    "    else\n",
    "        nbar = 2 .* (rand(N)) .+ 1\n",
    "        V = (hbar / 2) * diagm(vcat(nbar, nbar))\n",
    "        return S * V * transpose(S)  \n",
    "    end\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "219d6633-a937-4cbd-8417-f203c9524e1b",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    q_mat(cov; hbar=2)\n",
    "\n",
    "Compute the Husimi covariance matrix of a Gussian state.\n",
    "\n",
    "### Input\n",
    "- `cov`  -- Array: ``2N \\\\times 2N`` ``xp``-Wigner covariance matrix of the Gaussian state\n",
    "- `hbar` -- Float: (optional, default: `hbar=2`) value of ``\\\\hbar`` in the commutation relation ``[x,p]=i\\\\hbar``\n",
    "\n",
    "### Output\n",
    "\n",
    "Array: complex Husimi covariance matrix of the Gaussian state.\n",
    "\"\"\"\n",
    "function q_mat(cov; hbar=2)\n",
    "    n = size(cov)[1] ÷ 2\n",
    "    x = (2 / hbar) .* cov[1:n, 1:n]\n",
    "    xp = (2 / hbar) .* cov[1:n, n + 1:end]\n",
    "    p = (2 / hbar) .* cov[n + 1:end, n + 1:end]\n",
    "    ad = (x + p + im .* (xp - transpose(xp)) - 2 .* I) ./ 4\n",
    "    a = (x - p + im .* (xp + transpose(xp))) ./ 4\n",
    "    return [ad conj(a); a conj(ad)] + I\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "5e78efd7-2dfe-4bd2-9451-16893a803ffd",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    gen_omats(l, nbar; dtype=ComplexDF64)\n",
    "\n",
    "Generates the matrix O that enters inside the Torontonian for an l mode system\n",
    "in which the first mode is prepared in a thermal state with mean photon number nbar\n",
    "and the rest in vacuum and are later sent into a Fourier interferometer, i.e. one described\n",
    "by a DFT unitary matrix.\n",
    "\n",
    "### Input\n",
    "- `l`    -- Int: number of modes\n",
    "- `nbar` -- Float: mean photon number of the first mode (the only one not prepared in vacuum)\n",
    "- `dtype`-- (optional, default: `dtype=ComplexDF64`) type of the output matrix \n",
    "\n",
    "### Output\n",
    "\n",
    "Array: an O matrix whose Torontonian can be calculated analytically.\n",
    "\"\"\"\n",
    "function gen_omats(l, nbar; dtype=ComplexDF64)\n",
    "    A = (nbar / (l * (1.0 + nbar))) * ones(dtype, l, l)\n",
    "    O = [A 0*A ; 0*A A]\n",
    "    return O\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "2215ad50-519d-4d45-be46-a17280a94e51",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    analytical_tor(l, nbar)\n",
    "\n",
    "Return the value of the Torontonian of the O matrices generated by gen_omats.\n",
    "\n",
    "### Input\n",
    "- `l`    -- Int: number of modes\n",
    "- `nbar` -- Float: mean photon number of the first mode (the only one not prepared in vacuum)\n",
    "\n",
    "### Output\n",
    "\n",
    "Float: value of the torontonian of gen_omats(l, nbar).\n",
    "\"\"\"\n",
    "function analytical_tor(l, nbar)\n",
    "    if isapprox(l, nbar, atol=1e-14, rtol=0)\n",
    "        return 1.0\n",
    "    end\n",
    "    beta = -(nbar / (l * (1 + nbar)))\n",
    "    pref = factorial(l) / beta\n",
    "    p1 = (pref * l * gamma(1 / beta)) / gamma(1 / beta + l + 2)\n",
    "    p2 = (pref * beta * gamma(2 + 1 / beta)) / gamma(2 + 1 / beta + l)\n",
    "    return (p1 + p2) * ((-1) ^ l)\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "49550d77-fac9-48bc-85af-2fd7f77e43fb",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    quad_cholesky(L, Z, idx, mat)\n",
    "\n",
    "Returns the Cholesky factorization of a matrix using sub-matrix of prior\n",
    "Cholesky based on the new matrix and lower right quadrant.\n",
    "\n",
    "Algorithm from paper:\n",
    "https://arxiv.org/pdf/2109.04528.pdf\n",
    "\n",
    "### Input\n",
    "- `L`   -- Array: previous Cholesky factorization\n",
    "- `Z`   -- Array: new sub-matrix indices\n",
    "- `idx` -- Int: index of starting row/column of lower right quadrant\n",
    "- `mat` -- Array: new matrix\n",
    "\n",
    "### Output\n",
    "\n",
    "Array: the Cholesky factorization of matrix `mat`.\n",
    "\"\"\"\n",
    "function quad_cholesky(L, Z, idx, mat)\n",
    "    L = Array{ComplexDF64}(L)\n",
    "    mat = Array{ComplexDF64}(mat)\n",
    "    Ls = L[Z, Z]\n",
    "    lmat = size(mat)[1]\n",
    "    for i in idx:1:lmat\n",
    "        for j in idx:1:i-1\n",
    "            z = 0.0\n",
    "            for k in 1:1:j-1\n",
    "                z += Ls[i, k] * conj(Ls[j, k])\n",
    "            end\n",
    "            Ls[i, j] = (mat[i, j] - z) / Ls[j, j]\n",
    "        end\n",
    "        z = 0.0\n",
    "        for k in 1:1:i-1\n",
    "            z += Ls[i, k] * conj(Ls[i, k])\n",
    "        end\n",
    "        Ls[i, i] = real(sqrt(mat[i, i] - z))\n",
    "    end\n",
    "    return Ls\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "b73bbb8e-37ae-4604-a34c-22ff51de7387",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    recursiveTor(L, modes, A, n)\n",
    "\n",
    "Returns the recursive Torontonian sub-computation of a matrix.\n",
    "\n",
    "Algorithm from paper:\n",
    "https://arxiv.org/pdf/2109.04528.pdf\n",
    "\n",
    "### Input\n",
    "- `L`     -- Array: current Cholesky factorization\n",
    "- `modes` -- Array: optical modes\n",
    "- `A`     -- Array: a square, symmetric array of even dimensions\n",
    "- `n`     -- Int: size of the original matrix \n",
    "\n",
    "### Output\n",
    "\n",
    "Complex: the recursive Torontonian sub-computation of matrix `A`.\n",
    "\"\"\"\n",
    "function recursiveTor(L, modes, A, n)\n",
    "    L = Array{ComplexDF64}(L)\n",
    "    A = Array{ComplexDF64}(A)\n",
    "    tot = 0.0\n",
    "    if length(modes) == 0\n",
    "        start = 1\n",
    "    else\n",
    "        start = modes[end] + 1\n",
    "    end\n",
    "    for i in start:1:n\n",
    "        nextModes = vcat(modes, i)\n",
    "        nm = size(A)[1] ÷ 2\n",
    "        idx = 2 * (i - length(modes)) - 1\n",
    "        Z = vcat(collect(1:1:(idx - 1)), collect((idx + 2):1:(2 * nm)))\n",
    "        Az = A[Z, Z]\n",
    "        Ls = quad_cholesky(L, Z, idx, I - Az)\n",
    "        det = prod(diag(Ls)) .^ 2 \n",
    "        tot += ((-1) ^ length(nextModes)) / sqrt(det) + recursiveTor(Ls, nextModes, Az, n)\n",
    "    end\n",
    "    return tot\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "id": "1b07b821-5ef2-414f-8344-96f0c0ee8dec",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    tor(O)\n",
    "\n",
    "Returns the Torontonian of a matrix (using directly the definition).\n",
    "\n",
    "### Input\n",
    "- `O` -- Array: a square, symmetric array of even dimensions.\n",
    "\n",
    "### Output\n",
    "\n",
    "Complex: the Torontonian of matrix `O`.\n",
    "\"\"\"\n",
    "function tor(O)\n",
    "    O = Array{ComplexDF64}(O)\n",
    "    n = size(O)[1] ÷ 2\n",
    "    total = 0.0\n",
    "    for set in powerset(1:n)\n",
    "        pm_coeff = (-1) ^ (length(set))\n",
    "        kept_rows = vcat(set, set .+ n)\n",
    "        btt = sqrt(det(I - O[kept_rows, kept_rows]))\n",
    "        total += pm_coeff / btt\n",
    "    end\n",
    "    return ((-1) ^ n) * total\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "id": "575a1c9c-d878-4663-adcc-e021d6277ffc",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    rec_tor(A)\n",
    "\n",
    "Returns the Torontonian of a matrix (using the recursive algorithm).\n",
    "\n",
    "Algorithm from paper:\n",
    "https://arxiv.org/pdf/2109.04528.pdf\n",
    "\n",
    "### Input\n",
    "- `A` -- Array: a square, symmetric array of even dimensions.\n",
    "\n",
    "### Output\n",
    "\n",
    "Complex: the Torontonian of matrix `A`.\n",
    "\"\"\"\n",
    "function rec_tor(A)\n",
    "    A = Array{ComplexDF64}(A)\n",
    "    n = size(A)[1] ÷ 2\n",
    "    Z = Array{Int64}(undef, 2 * n)\n",
    "    Z[1:2:end] = collect(1:1:n)\n",
    "    Z[2:2:end] = collect((n + 1):1:(2 * n))\n",
    "    A = A[Z, Z]\n",
    "    L = cholesky(I - A).L\n",
    "    det = prod(diag(L)) .^ 2\n",
    "    return 1 / sqrt(det) + recursiveTor(L, Array{Int64}(undef, 0), A, n) \n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "6b34d52c-55a8-45b1-9241-275918ee127e",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    threshold_detection_prob(cov, det_pattern; hbar=2, recursive=true)\n",
    "\n",
    "Returns threshold detection probabilities for Gaussian states without displacement.\n",
    "\n",
    "### Input\n",
    "- `cov`         -- Array: 2D xp Wigner covariance matrix\n",
    "- `det_pattern` -- Array: 1D array of {0,1} that describes the threshold detection outcome\n",
    "- `hbar`        -- Float: (optional, default: `hbar=2`) value of ``\\\\hbar`` in the \n",
    "                          commutation relation ``[x,p]=i\\\\hbar``\n",
    "- `recursive`   -- Bool: (optional, default: `recursive=true`) indicates whether to use the \n",
    "                        recursive algorithm for the computation of the Torontonian or not\n",
    "\n",
    "### Output\n",
    "\n",
    "Float: probability of the detction pattern.\n",
    "\"\"\"\n",
    "function threshold_detection_prob(cov, det_pattern; hbar=2, recursive=true)\n",
    "    cov = Array{ComplexDF64}(cov)\n",
    "    n = size(cov)[1] ÷ 2\n",
    "    qcov = q_mat(cov, hbar=hbar)\n",
    "    O = I - inv(qcov)\n",
    "    rpatt = vcat(det_pattern, det_pattern)\n",
    "    rows = findall(x -> x != 0, rpatt)\n",
    "    rO = O[rows, rows]\n",
    "    if recursive\n",
    "        return rec_tor(Hermitian(rO)) / sqrt(det(qcov))\n",
    "    else\n",
    "        return tor(rO) / sqrt(det(qcov))\n",
    "    end\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "02159133-f3b9-4fd6-8f68-5809d1fe2ad1",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[0m\u001b[1mTest Summary:                                       | \u001b[22m\u001b[32m\u001b[1mPass  \u001b[22m\u001b[39m\u001b[36m\u001b[1mTotal\u001b[22m\u001b[39m\n",
      "Torontonian (from definition) vs. analytical result | \u001b[32m 204  \u001b[39m\u001b[36m  204\u001b[39m\n"
     ]
    }
   ],
   "source": [
    "#checking that the Torontonian function (from definition) gives the correct analytical results\n",
    "@testset \"Torontonian (from definition) vs. analytical result\" begin\n",
    "    for i = 2:1:13\n",
    "        for k = 1.1:0.37:7.3\n",
    "            @test isapprox(analytical_tor(i, k), tor(gen_omats(i, k)))\n",
    "        end\n",
    "    end\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "id": "67473db6-7ce2-412e-a514-15a2a58192f2",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[0m\u001b[1mTest Summary:                                 | \u001b[22m\u001b[32m\u001b[1mPass  \u001b[22m\u001b[39m\u001b[36m\u001b[1mTotal\u001b[22m\u001b[39m\n",
      "Torontonian (recursive) vs. analytical result | \u001b[32m 204  \u001b[39m\u001b[36m  204\u001b[39m\n"
     ]
    }
   ],
   "source": [
    "#checking that the Torontonian function (recursive) gives the correct analytical results\n",
    "@testset \"Torontonian (recursive) vs. analytical result\" begin\n",
    "    for i = 2:1:13\n",
    "        for k = 1.1:0.37:7.3\n",
    "            @test isapprox(analytical_tor(i, k), rec_tor(gen_omats(i, k)))\n",
    "        end\n",
    "    end\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "id": "c74b70b5-9ab6-4d1f-947f-7b0e8c6d9519",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "pattern | prob (definition) | prob (recursive)\n",
      "[0, 0, 0, 0] | 0.06507496410834357 | 0.06507496410834357\n",
      "[1, 0, 0, 0] | 0.0034324592075896045 | 0.0034324592075896045\n",
      "[0, 1, 0, 0] | 0.009999227670283547 | 0.009999227670283547\n",
      "[1, 1, 0, 0] | 0.038959279561609404 | 0.038959279561609404\n",
      "[0, 0, 1, 0] | 0.013435752178618783 | 0.013435752178618783\n",
      "[1, 0, 1, 0] | 0.026273905343936928 | 0.026273905343936914\n",
      "[0, 1, 1, 0] | 0.009760054407607618 | 0.009760054407607618\n",
      "[1, 1, 1, 0] | 0.2426663801923161 | 0.2426663801923156\n",
      "[0, 0, 0, 1] | 0.021998987775370214 | 0.021998987775370214\n",
      "[1, 0, 0, 1] | 0.002473645468522954 | 0.0024736454685229544\n",
      "[0, 1, 0, 1] | 0.01266520930503906 | 0.012665209305039062\n",
      "[1, 1, 0, 1] | 0.02337750291328705 | 0.02337750291328706\n",
      "[0, 0, 1, 1] | 0.01602701184944208 | 0.016027011849442083\n",
      "[1, 0, 1, 1] | 0.02009929406904555 | 0.020099294069045523\n",
      "[0, 1, 1, 1] | 0.020021633341405824 | 0.020021633341405824\n",
      "[1, 1, 1, 1] | 0.4737346926075817 | 0.4737346926075797\n"
     ]
    }
   ],
   "source": [
    "#Computing threshold detection probabilities\n",
    "N = 4 #number of modes\n",
    "cov = random_covariance(N) #rendom covariance matrix\n",
    "println(\"pattern\", \" | \", \"prob (definition)\", \" | \", \"prob (recursive)\")\n",
    "for p in product([[0 1] for i = 1:N]...) #this line generates all possible detection patterns\n",
    "    pattern = collect(p)\n",
    "    prob_df = threshold_detection_prob(cov, pattern, recursive = false)\n",
    "    prob_rc = threshold_detection_prob(cov, pattern, recursive = true)\n",
    "    println(pattern, \" | \", real(prob_df), \" | \", real(prob_rc))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "da7716be-c216-4a8a-bcfe-a1bdea702b80",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.3",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
